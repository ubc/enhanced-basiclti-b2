apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"
apply plugin: "eclipse"
apply plugin: "idea"

// Uncomment if your project has an API JAR
//apply from: "api.gradle"

// define the version for the project when publishing to maven
group "ca.ubc"
version "3.0.5001"

webAppDirName = file("web")
sourceCompatibility = '1.7'
targetCompatibility = '1.7'

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'src/resources'
        }
    }

    test {
        java {
            srcDir 'test/java'
        }
        resources {
            srcDir 'test/resources'
        }
    }
}

ext {
    learnVersion = "9.1.201410.160373"
    deployServer = "137.82.12.113"
    deployInstance = "BBLEARN"
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.blackboard.com/content/repositories/releases/"
    }
    maven {
        url "http://artifactory.ctlt.ubc.ca/artifactory/ctlt-release-local/"
    }
}

configurations {
    buildUtils
}

// define the project's dependencies
dependencies {
    providedCompile "javax.servlet:servlet-api:2.5",
            "javax.servlet.jsp:jsp-api:2.1"

    // Dependencies are libraries needed to build, but should not be included in the B2 WAR.
    // You should NEVER include Learn JARs (other than webapis) in your B2.
    providedCompile("blackboard.platform:bb-platform:$ext.learnVersion") { transitive = false }
    providedCompile("blackboard.platform:bb-taglibs:$ext.learnVersion") { transitive = false }

    // Building a Spring B2?  Use our Spring APIs!
    // compile "blackboard.platform:bb-spring-webapi:10.1"

    // Include the Blackboard security APIs for protecting your B2.
    // providedCompile "blackboard.plugin:security:1.0:api"

    // Dependencies that are only necessary at runtime so should be included in the WAR
    // runtime "org.javassist:javassist:3.17.1-GA"

    buildUtils "org.oscelot:b2deploy-task:0.1.0"

    compile "com.spvsoftwareproducts:b2context:1.4.01",
			'org.oscelot.blackboard:lti-assessment:1.0',
            'commons-httpclient:commons-httpclient:3.1',
            'org.jdom:jdom:1.1.3',
            'net.sf.json-lib:json-lib:2.4:jdk15',
            'net.oauth.core:oauth-provider:20100527',
            'com.google.code.gson:gson:2.2.4'
    runtime "com.spvsoftwareproducts:b2context:1.3.00",
            'commons-httpclient:commons-httpclient:3.1',
            'org.jdom:jdom:1.1.3',
            'net.sf.json-lib:json-lib:2.4:jdk15',
            'net.oauth.core:oauth-provider:20100527'
    testCompile 'org.testng:testng:6.8.7'
}

test {
    useTestNG()
}

// Add a task to deploy a B2 using starting block
task deployB2(dependsOn: "war") << {
    ant.taskdef(name: "b2deploy", classname: "org.oscelot.ant.B2DeployTask", classpath: project.configurations.buildUtils.asPath)
    ant.b2deploy(localfilepath: project.war.archivePath, host: project.ext.deployServer, courseorgavailable: 'true', clean: 'true', webappName: 'bb-starting-block-' + project.ext.deployInstance)
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}
